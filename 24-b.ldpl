data:

auxText is text
lines is text list
grid is text list list
grid2 is text list list
i is number
j is number
count is number
deltas is number list list
delta is number list
di is number
dj is number
grids is text list list list
grids2 is text list list list
gen is number
level is number
levele is number
e is number

procedure:

load file "24.txt" in auxText
trim auxText in auxText
split auxText by crlf in lines

for i from 0 to 5 step 1 do
    push list to grid
    for j from 0 to 5 step 1 do
        push "." to grid:i
    repeat
repeat
store "?" in grid:2:2

for level from 0 to 501 step 1 do
    push list to grids
    copy grid to grids:level
repeat

clear grid
store 0 in i
for each auxText in lines do
    push list to grid
    split auxText by "" in grid:i
    in i solve i + 1
repeat
store "?" in grid:2:2

copy grid to grids:250

push list to deltas
push list to deltas
push list to deltas
push list to deltas
push -1 to deltas:0
push 0 to deltas:0
push 0 to deltas:1
push 1 to deltas:1
push 1 to deltas:2
push 0 to deltas:2
push 0 to deltas:3
push -1 to deltas:3

copy grids to grids2

for gen from 0 to 200 step 1 do
    for level from 1 to 500 step 1 do
        for i from 0 to 5 step 1 do
            for j from 0 to 5 step 1 do
                store 0 in count
                for each delta in deltas do
                    in di solve i + delta:1
                    in dj solve j + delta:0
                    if di is equal to -1 then
                        in levele solve level - 1
                        if grids:levele:1:2 is equal to "#" then
                            in count solve count + 1
                        end if
                    else if di is equal to 5 then
                        in levele solve level - 1
                        if grids:levele:3:2 is equal to "#" then
                            in count solve count + 1
                        end if
                    else if dj is equal to -1 then
                        in levele solve level - 1
                        if grids:levele:2:1 is equal to "#" then
                            in count solve count + 1
                        end if
                    else if dj is equal to 5 then
                        in levele solve level - 1
                        if grids:levele:2:3 is equal to "#" then
                            in count solve count + 1
                        end if
                    else if grids:level:di:dj is equal to "?" then
                        in levele solve level + 1
                        for e from 0 to 5 step 1 do
                            if i is equal to 1 then
                                if grids:levele:0:e is equal to "#" then
                                    in count solve count + 1
                                end if
                            else if i is equal to 3 then
                                if grids:levele:4:e is equal to "#" then
                                    in count solve count + 1
                                end if
                            else if j is equal to 1 then
                                if grids:levele:e:0 is equal to "#" then
                                    in count solve count + 1
                                end if
                            else if j is equal to 3 then
                                if grids:levele:e:4 is equal to "#" then
                                    in count solve count + 1
                                end if
                            end if
                        repeat
                    else if grids:level:di:dj is equal to "#" then
                        in count solve count + 1
                    end if
                repeat
                if grids:level:i:j is equal to "#" and count is not equal to 1 then
                    store "." in grids2:level:i:j
                else if grids:level:i:j is equal to "." and ( count is equal to 1 or count is equal to 2 ) then
                    store "#" in grids2:level:i:j
                end if
            repeat
        repeat
    repeat
    copy grids2 to grids
repeat

store 0 in count
for level from 0 to 501 step 1 do
    for i from 0 to 5 step 1 do
        for j from 0 to 5 step 1 do
            if grids:level:i:j is equal to "#" then
                in count solve count + 1
            end if
        repeat
    repeat
repeat

display count crlf