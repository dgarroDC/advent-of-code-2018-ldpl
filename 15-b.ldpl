using package std-list

data:

auxText is text
programList is text list
auxNumber is number
program is text map
pc is text
inputBuffer is text list
outputBuffer is text list
relBase is text
explored is text map map
deltas is number list list
used is number
size is number
programs is text map list
pcs is text list
relBases is text list
i is number
steps is number
stepss is number list
x is number
y is number
xs is number list
ys is number list

procedure:

load file "15.txt" in auxText
split auxText by "," in programList

store 0 in auxNumber
for each auxText in programList do
    trim auxText in auxText
    store auxText in program:auxNumber
    in auxNumber solve auxNumber + 1
repeat

push list to deltas
push list to deltas
push list to deltas
push list to deltas
push list to deltas
push 0 to deltas:1
push -1 to deltas:1
push 0 to deltas:2
push 1 to deltas:2
push -1 to deltas:3
push 0 to deltas:3
push 1 to deltas:4
push 0 to deltas:4

push map to programs
copy program to programs:0
push "0" to pcs
push "0" to relBases
push 0 to xs
push 0 to ys
push 0 to stepss

while 0 is equal to 0 do
    in steps solve steps + 1
    for i from 1 to 5 step 1 do
        copy programs:used to program
        store pcs:used in pc
        store relBases:used in relBase
        store xs:used in x
        store ys:used in y
        in steps solve stepss:used + 1

        in x solve x + deltas:i:0
        in y solve y + deltas:i:1
        if explored:x:y is equal to "1" then
            continue
        end if
        store "1" in explored:x:y
        store i in auxText
        clear inputBuffer
        push auxText to inputBuffer
        clear outputBuffer
        call runProgram with program pc inputBuffer outputBuffer 0 relBase 0
        if outputBuffer:0 is equal to "2" then
            clear programs
            clear pcs
            clear relBases
            clear xs
            clear ys
            clear stepss
            clear explored
            store "1" in explored:x:y
            push map to programs
            store 0 in size
            copy program to programs:size
            push pc to pcs
            push relBase to relBases
            push x to xs
            push y to ys
            push 0 to stepss
            store -1 in used
        else if outputBuffer:0 is equal to "1" then
            push map to programs
            in size solve size + 1
            copy program to programs:size
            push pc to pcs
            push relBase to relBases
            push x to xs
            push y to ys
            push steps to stepss
        end if
    repeat
    in used solve used + 1
    get length of pcs in auxNumber
    if used is greater than or equal to auxNumber then
        in steps solve steps - 1
        display steps crlf
        exit
    end if
repeat

sub runProgram
parameters:
    program is text map
    pc is text
    inputBuffer is text list
    outputBuffer is text list
    inputCount is number
    relBase is text
    haltSignal is number
local data:
    opcode is text
    mode0 is text
    mode1 is text
    mode2 is text
    inA is text
    inB is text
    out is text
    auxNumber is number
procedure:
    while 0 is equal to 0 do
        store program:pc in opcode
        if opcode is equal to "" then
            store "0" in opcode
        end if
        call sumBig with pc "1" pc

        while 0 is equal to 0 do  # Pad with 0
            get length of opcode in auxNumber
            if auxNumber is less than 5 then
                in opcode join "0" opcode
            else
                break
            end if
        repeat

        get character at 0 from opcode in mode2
        get character at 1 from opcode in mode1
        get character at 2 from opcode in mode0
        substring opcode from 3 length 2 in opcode
        if opcode is equal to "99" then
            store 1 in haltSignal
            return
        else if opcode is not equal to "03" then
            call readParameter with mode0 program pc relBase inA
            call sumBig with pc "1" pc
            if opcode is equal to "04" then
                push inA to outputBuffer
            else if opcode is equal to "09" then
                call sumBig with relBase inA relBase
            else
                call readParameter with mode1 program pc relBase inB
                call sumBig with pc "1" pc
                if opcode is equal to "01" then
                    call sumBig with inA inB out
                else if opcode is equal to "02" then
                    call mulBig with inA inB out
                else if opcode is equal to "05" then
                    if inA is not equal to "0" then
                        store inB in pc
                    end if
                    continue
                else if opcode is equal to "06" then
                    if inA is equal to "0" then
                        store inB in pc
                    end if
                    continue
                else if opcode is equal to "07" then
                    call lessBig with inA inB out
                else if opcode is equal to "08" then
                    if inA is equal to inB then
                        store 1 in out
                    else
                        store 0 in out
                    end if
                end if
                if program:pc is equal to "" then
                    store 0 in program:pc
                end if
                call saveParameter with mode2 program pc relBase out
                call sumBig with pc "1" pc
            end if
        else  # 03
            get length of inputBuffer in auxNumber
            if inputCount is less than auxNumber then
                if program:pc is equal to "" then
                    store 0 in program:pc
                end if
                call saveParameter with mode0 program pc relBase inputBuffer:inputCount
                call sumBig with pc "1" pc
                in inputCount solve inputCount + 1
            else
                call sumBig with pc "-1" pc
                return
            end if
        end if
    repeat
end sub

sub readParameter
parameters:
    mode is text
    program is text map
    pc is text
    relBase is text
    result is text
procedure:
    if mode is equal to "0" then
        call readParameter with "2" program pc "0" result
    else if mode is equal to "1" then
        store program:pc in result
    else
        call readParameter with "1" program pc relBase result
        call sumBig with result relBase result
        store program:result in result
    end if
    if result is equal to "" then
        store "0" in result
    end if
end sub

sub saveParameter
parameters:
    mode is text
    program is text map
    pc is text
    relBase is text
    save is text
local data:
    result is text
procedure:
    if mode is equal to "0" then
        call saveParameter with "2" program pc "0" save
    else
        call readParameter with "1" program pc relBase result
        call sumBig with result relBase result
        store save in program:result
    end if
end sub

sub sumBig
parameters:
    a is text
    b is text
    res is text
local data:
	resNum is number
procedure:
    in resNum solve a + b
    store resNum in res
end sub

sub mulBig
parameters:
    a is text
    b is text
    res is text
local data:
	resNum is number
procedure:
    in resNum solve a * b
    store resNum in res
end sub

sub lessBig
parameters:
    a is text
    b is text
    res is text
local data:
	aNum is number
	bNum is number
procedure:
	store a in aNum
	store b in bNum
    if aNum is less than bNum then
    	store "1" in res
    else
    	store "0" in res
    end if
end sub